% ЭЛЕКТРОННЫЙ  ЗАДАЧНИК  ПО  ЛОГИЧЕСКОМУ  ПРОГРАММИРОВАНИЮ.
% Версия 1.0 для системы PROLOGUS  
% 
%                   Copyright (С) 1993  Боголюбов Д.П. (МИЭМ).  
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%               У Р О К    3
%   База знаний "Русская патриархальная семья"      
%               Ч А С Т Ь   1
%      тема: Сложно-составные вопросы
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
% Напишем программу, анализирующую родственные отношения
% в большой семье.
%
% Допустим, есть семья, состоящая из пяти человек, и родственные
% связи между ними  можно представить  следующей схемой:
%
%  ┌──────────────────────────────────────────────────────────┐
%  │   Петр    брат  брат    Иван      родители      Ольга    │
%  │ Петрович  <-------->  Петрович  <---------->  Николаевна │
%  │                                      !                   │
%  │                                      !                   │
%  │                                 сестра  брат             │
%  │                           Маша  <----------> Миша        │
%  │                                     дети                 │
%  └──────────────────────────────────────────────────────────┘
%
% Начать  описание  такой  семьи  можно   с   описания
% отношения "родитель".

  родитель(Иван_Петрович,Маша).
  родитель(Иван_Петрович,Миша).    
  родитель(Ольга_Николаевна,Миша).      
  родитель(Ольга_Николаевна,Маша).

% Используя эту начальную группу фактов, попытаемся определить
% правило для отношения отец. Отец это, во-первых, родитель, а,
% во-вторых, мужчина. Учитывая все вышесказанное:

  отец(_некто,_ребенок):-родитель(_некто,_ребенок),
                        пол(_некто,мужской).

% Однако, задав вопрос типа ?-отец(Иван_Петрович,Маша) мы получим 
% ответ НЕТ, поскольку вторая подцель в правиле является ложной
% (предложение с именем "мужчина" вообще отсутствует в программе).
% Поэтому для полноты картины введем аналогичные факты для
% всех членов семьи.

  пол(Петр_Петрович,мужской).
  пол(Иван_Петрович,мужской).
  пол(Ольга_Николаевна,женский).
  пол(Миша,мужской).
  пол(Маша,женский).
  
% Теперь отношение отец стало вполне корректным.   
% Аналогично можно определить отношение мать.  
  
  мать(_некто,_ребенок):-родитель(_некто,_ребенок),
                         пол(_некто,женский).
  
  
% Правила для отношений "дочь" и "сын" очень похожи на  два
% предыдущих правила. Например, правило для отношения "дочь":
% _X - дочь _Y, если _Y - родитель _Х, и _Х принадлежит к женской
% половине человечества.

  дочь(_X,_Y) :- родитель(_Y,_X),пол(_X,женский).
  сын(_X,_Y) :- родитель(_Y,_X),пол(_X,мужской).

% Если два человека  являются  родителями  кого-то,  то
% логично предположить  (берем стандартную семью), что  они
% являются также и супругами.

  муж(_он,_она) :- отец(_он,_Реб),мать(_она,_Реб).
  жена(_она,_он) :- мать(_она,_Реб),отец(_он,_Реб).

% Следующие отношения, которые мы рассмотрим, есть брат и сестра.

  брат(_X,_Y):-мать(_Z,_X),мать(_Z,_Y),пол(_X,мужской),_X \= _Y.
  сестра(_X,_Y):-мать(_Z,_X),мать(_Z,_Y),пол(_X,женский),_X \= _Y.

% Первое правило гласит, что _X является братом _Y,  если
% они оба имеют одних и тех же родителей (мать в частности),
% и _X обязательно должен быть  мужчиной (иначе  какой же  он
% брат?). Последнее условие необходимо, чтобы _Х не  оказался
% братом самому себе.  Второе  правило  целиком   аналогично
% первому и никаких новых пояснений здесь не требуется.

% Это правило не описывает полностью всех братьев данной семьи.
% Осталось еще ввести два факта:

  брат(Петр_Петрович,Иван_Петрович).
  брат(Иван_Петрович,Петр_Петрович).
  
% То, что их два, связано с тем, что первый аргумент означает
% "кто есть брат", а второй - "для кого".   
%
% Теперь можно поэкспериментировать  с  вопросами   к 
% построенной базе данных.

% Например, выясним  -  кто имеет  брата,  женатого  на
% Ольге Николаевне?
% Такой вопрос можно задать следующим образом:

  ?-брат(_X,_Y),муж(_Y,Ольга_Николаевна).

% Это - сложный вопрос, состоящий из двух подцелей.
% Поиск ответа на него соответствует поиску по И-дереву, то
% есть такая цель будет считаться достигнутой, если будут 
% достигнуты сначала первая, а затем и вторая подцели.
 
% Естественно, _X = Петр Петрович, а _Y = Иван  Петрович,
% то  есть  у Петра  Петровича  есть  брат,  удовлетворяющий
% условиям запроса. Кстати, обратите внимание на то, что, 
% если вы запустите программу в режиме поиска всех решений,
% то получите два совершенно одинаковых ответа.
% Выясните, почему их именно два (нарисуйте дерево вывода).
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%  Конец  Части 1  Урока 3.
%  Загрузите файл  PR3_2.PRO ( Часть 2 Урока 3 )
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

